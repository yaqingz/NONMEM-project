$PROBLEM PD final
$INPUT ID TIME DV AMT TYPE
$DATA dopamin.dta IGNORE=@
$SUBROUTINE ADVAN6 TOL=5
$MODEL COMP=(DEPOT,DEFDOSE)
COMP=(CENTRAL)
COMP=(RESPONSE)

$PK
TVCL = THETA(1)
TVV = THETA(2)TVKA = THETA(3)
TVSLOPE = THETA(4)
TVKOUT = THETA(5)
TVBASE = THETA(6)
;BSV
CL = TVCL*EXP(ETA(1))
V = TVV*EXP(ETA(2))
KA = TVKA*EXP(ETA(3))
KE=CL/V
SLOPE = TVSLOPE*EXP(ETA(4))
KOUT = TVKOUT*EXP(ETA(5))
BASE = TVBASE * EXP(ETA(6))
KIN = KOUT * BASE
A_0(3)=BASE

$DES
DADT(1) = -KA*A(1)
DADT(2) = KA*A(1)-KE*A(2)
CONC = A(2)/V
DADT(3) = KIN*(1+ (SLOPE*CONC))-KOUT*A(3)

$ERROR
IPRED = A(2)/V
Y = IPRED * (1+EPS(1)) ; proportion RUV for pk
IF(TYPE.EQ.0) THEN
IPRED = A(3)
Y = IPRED *(1 + EPS(2)) ; proportion RUV for pd
END IF
IRES = DV - IPRED
W = SQRT(SIGMA(1,1)*IPRED**2)
IF(TYPE.EQ.0) W = SQRT(SIGMA(2,2)*IPRED**2 )
IF(W.EQ.0) W = 1
IWRES = IRES/W

$THETA 0.0114216 ; 1. CL
$THETA 1.05796 ; 2. V
$THETA 0.06606 ; 3.KA
$THETA 177.458 ; 4.SLOPE
$THETA (0,0.108913) ; 5.KOUT
$THETA (0,0.0767626) ; 6.BASE

$OMEGA 0.0638381 ; 1.CL
0.0314456 ; 2.V
0 FIX ; 3.KA_
0.133577 ; 4.SLOPE : EXPONENTIAL
1.13857 ; 5. KOUT
0.181002 ; 6. BASE

$SIGMA 0.0245573 ; 1.PK : PROPORTIONAL RUV

$SIGMA 0.157628 ; 2. PD : PROPORTIONAL RUV

$ESTIMATION METHOD=1 INTER POSTHOC

$COVARIANCE